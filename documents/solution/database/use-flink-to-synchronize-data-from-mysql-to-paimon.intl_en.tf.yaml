ROSTemplateFormatVersion: '2015-09-01'
Transform: Aliyun::Terraform-v1.5
Metadata:
  ALIYUN::ROS::Interface:
    TemplateTags:
      - acs:technical-solution:data-analysis:Flink CDC for Enterprise-Level Real-Time Data Synchronization-tech_solu_183
Workspace:
  variable.tf: |-
    # ------------------------------------------------------------------------------
    # Module Input Variables
    #
    # This file defines all configurable input variables for this Terraform module.
    # Each variable contains detailed descriptions to help users configure the module correctly.
    # ------------------------------------------------------------------------------

    # Instance type for the RDS
    variable "dbinstance_class" {
      type        = string
      description = "RDS instance specification"
      default     = "mysql.n2.medium.1"
    }

    # Account name for the RDS database
    variable "db_user_name" {
      type        = string
      default     = "db_user"
      nullable    = false
      description = "RDS database account"
    }

    # Password for the RDS database
    variable "db_password" {
      type        = string
      sensitive   = true
      description = "RDS database password"
      #default    = ""
    }

    # Name of the OSS bucket
    variable "bucket_name" {
      type        = string
      description = "OSS bucket name"
      default     = "flink-cdc"
    }

    # Directory name within the OSS bucket
    variable "directory_name" {
      type        = string
      default     = "warehouse"
      nullable    = false
      description = "Bucket directory name"
    }

    # Name of the Flink instance
    variable "flink_instance_name" {
      type        = string
      description = "Flink instance name"
      default     = "flink-cdc-test"
    }

  main.tf: |-
    # ------------------------------------------------------------------------------
    # Main Resource Definitions
    # 
    # This file contains the core infrastructure resources of the module.
    # The code here is responsible for creating and configuring all cloud resources based on input variables.
    # ------------------------------------------------------------------------------

    # Configure the Alibaba Cloud provider
    provider "alicloud" {
      region = "ap-southeast-1"
    }

    resource "random_string" "lowercase" {
      length  = 8
      special = false
      upper   = false
      numeric = false
    }

    # Local variable, resource name prefix
    locals {
      common_name = "flink-cdc-${random_string.lowercase.result}"
    }

    # Query availability zones
    data "alicloud_db_zones" "zones_ids" {
      engine                   = "MySQL"
      engine_version           = "8.0"
      instance_charge_type     = "PostPaid"
      category                 = "Basic"
      db_instance_storage_type = "cloud_essd"
    }

    # Enable OSS service
    data "alicloud_oss_service" "open_oss" {
      enable = "On"
    }

    # Create a VPC
    resource "alicloud_vpc" "vpc" {
      vpc_name   = "vpc-${local.common_name}"
      cidr_block = "192.168.0.0/16"
    }

    # Create a VSwitch
    resource "alicloud_vswitch" "vswitch" {
      vpc_id       = alicloud_vpc.vpc.id
      cidr_block   = "192.168.1.0/24"
      vswitch_name = "test-vsw"
      zone_id      = data.alicloud_db_zones.zones_ids.ids.1
    }

    # Create an RDS instance
    resource "alicloud_db_instance" "rdsdbinstance" {
      zone_id                  = data.alicloud_db_zones.zones_ids.ids.1
      vpc_id                   = alicloud_vpc.vpc.id
      vswitch_id               = alicloud_vswitch.vswitch.id
      instance_type            = var.dbinstance_class
      instance_storage         = 50
      engine                   = "MySQL"
      engine_version           = "8.0"
      category                 = "Basic"
      db_instance_storage_type = "cloud_essd"
      security_ips             = ["192.168.0.0/16"]
      released_keep_policy     = "None"
    }

    # Create an RDS account
    resource "alicloud_rds_account" "account" {
      db_instance_id   = alicloud_db_instance.rdsdbinstance.id
      account_type     = "Super"
      account_password = var.db_password
      account_name     = var.db_user_name
    }

    # Create databases
    resource "alicloud_db_database" "db_base1" {
      instance_id   = alicloud_db_instance.rdsdbinstance.id
      character_set = "utf8"
      name          = "tpc_ds"
    }

    resource "alicloud_db_database" "db_base2" {
      instance_id   = alicloud_db_instance.rdsdbinstance.id
      character_set = "utf8"
      name          = "user_db1"
    }

    resource "alicloud_db_database" "db_base3" {
      instance_id   = alicloud_db_instance.rdsdbinstance.id
      character_set = "utf8"
      name          = "user_db2"
    }

    resource "alicloud_db_database" "db_base4" {
      instance_id   = alicloud_db_instance.rdsdbinstance.id
      character_set = "utf8"
      name          = "user_db3"
    }

    # Create an OSS bucket
    resource "alicloud_oss_bucket" "bucket" {
      bucket          = "${var.bucket_name}-${random_string.lowercase.result}"
      force_destroy = true
    }

    # Create an OSS bucket object
    resource "alicloud_oss_bucket_object" "directory_name" {
      bucket        = alicloud_oss_bucket.bucket.bucket
      key           = "${replace(trimspace(var.directory_name), "/$", "")}/"
      content       = "Directory placeholder. Managed by Terraform."
      acl           = "private"
    }

    # Create a Flink instance
    resource "alicloud_realtime_compute_vvp_instance" "flink_instance" {
      zone_id                  = data.alicloud_db_zones.zones_ids.ids.1
      vpc_id                   = alicloud_vpc.vpc.id
      vswitch_ids              = [
        alicloud_vswitch.vswitch.id
      ]
      vvp_instance_name        = var.flink_instance_name
      payment_type             = "PayAsYouGo"
      storage {
        oss {
          bucket = alicloud_oss_bucket.bucket.bucket
        }
      }
    }
  output.tf: |-
    # ------------------------------------------------------------------------------
    # Module Outputs
    #
    # This file defines the values returned to the caller after successful module execution.
    # These outputs can be referenced by other Terraform configurations or displayed to users after the apply command completes.
    # ------------------------------------------------------------------------------
    
    # Outputs the RDS instance id.
    output "rds_instance_id" {
      value       = alicloud_db_instance.rdsdbinstance.id
      description = "RDS instance id"
    }

    # Outputs the internal connection address of the RDS.
    output "mysql_host" {
      value       = alicloud_db_instance.rdsdbinstance.connection_string
      description = "Internal connection address"
    }

    # Outputs the database username.
    output "db_username" {
      value       = var.db_user_name
      description = "Database username"
    }

    # Outputs the database password.
    output "db_password" {
      value       = var.db_password
      sensitive   = true
      description = "Database password"
    }

    # Outputs the OSS bucket name.
    output "bucket_name" {
      value       = alicloud_oss_bucket.bucket.bucket
      description = "OSS bucket name"
    }

    # Outputs the OSS directory prefix.
    output "oss_prefix" {
      value       = var.directory_name
      description = "OSS object key prefix"
    }
