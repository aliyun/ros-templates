ROSTemplateFormatVersion: '2015-09-01'
Transform: Aliyun::Terraform-v1.5
Metadata:
  ALIYUN::ROS::Interface:
    TemplateTags:
      - acs:technical-solution:db:One-Stop Spatio-Temporal Decision-Making, Unlocking Spatial Data Value-tech_solu_267
Workspace:
  variable.tf: |-
    # ------------------------------------------------------------------------------
    # Module Input Variables
    #
    # This file defines all configurable input variables for this Terraform module.
    # Each variable contains detailed descriptions to help users configure the module correctly.
    # ------------------------------------------------------------------------------

    # RDS instance class
    variable "db_instance_class" {
      type        = string
      default     = "pg.n4.2c.1m"
      nullable    = false
      description = "Instance specification"
    }

    # RDS database username
    variable "rds_db_user" {
      type        = string
      default     = "test_user"
      nullable    = false
      description = "RDS database account"
    }

    # RDS database name
    variable "db_name" {
      type        = string
      default     = "food_test"
      nullable    = false
      description = "RDS database name"
    }

    # RDS database password
    variable "db_password" {
      type        = string
      sensitive   = true
      description = "RDS database password"
      #default    = ""
    }
  main.tf: |-
    # ------------------------------------------------------------------------------
    # Main Resource Definitions
    # 
    # This file contains the core infrastructure resources of the module.
    # The code here is responsible for creating and configuring all cloud resources based on input variables.
    # ------------------------------------------------------------------------------

    # Configure Alibaba Cloud provider
    provider "alicloud" {
      region = "cn-hangzhou"
    }

    # Generate random ID for resource naming
    resource "random_id" "suffix" {
      keepers = {
        user = var.rds_db_user
      }
      byte_length = 4
    }

    # Define local variables
    locals {
      instance_name = "datav-${var.rds_db_user}-${random_id.suffix.hex}"
      connection_prefix = "datav-pg-${random_id.suffix.hex}"
    }

    # Query availability zones
    data "alicloud_db_zones" "default" {
      engine                   = "PostgreSQL"
      engine_version           = "17.0"
      instance_charge_type     = "PostPaid"
      category                 = "Basic"
      db_instance_storage_type = "cloud_essd"
    }

    # Create VPC
    resource "alicloud_vpc" "vpc" {
      vpc_name   = "VPC_HZ_${random_id.suffix.hex}"
      cidr_block = "192.168.0.0/16"
    }

    # Create VSwitch
    resource "alicloud_vswitch" "vswitch" {
      vpc_id       = alicloud_vpc.vpc.id
      cidr_block   = "192.168.1.0/24"
      vswitch_name = "vsw_001"
      zone_id      = data.alicloud_db_zones.default.zones[0].id
    }

    # Create PostgreSQL instance
    resource "alicloud_db_instance" "db_instance" {
      engine                   = "PostgreSQL"
      engine_version           = "17.0"
      instance_storage         = "50"
      instance_type            = var.db_instance_class
      instance_charge_type     = "Postpaid"
      instance_name            = local.instance_name
      zone_id                  = data.alicloud_db_zones.default.zones[0].id
      vswitch_id               = alicloud_vswitch.vswitch.id
      db_instance_storage_type = "cloud_essd"
      category                 = "Basic"
      security_ips             = ["47.99.0.0/16", "192.168.0.0/16"]
    }

    # Create public connection
    resource "alicloud_db_connection" "public" {
      instance_id       = alicloud_db_instance.db_instance.id
      connection_prefix = local.connection_prefix
      port              = "5432"
    }

    # Create RDS account
    resource "alicloud_rds_account" "account" {
      db_instance_id   = alicloud_db_instance.db_instance.id
      account_type     = "Super"
      account_password = var.db_password
      account_name     = var.rds_db_user
    }

    # Create RDS database
    resource "alicloud_db_database" "db_database" {
      instance_id   = alicloud_db_instance.db_instance.id
      character_set = "utf8"
      name          = var.db_name
    }

    # Set RDS account privileges
    resource "alicloud_db_account_privilege" "privilege" {
      instance_id  = alicloud_db_instance.db_instance.id
      account_name = alicloud_rds_account.account.account_name
      privilege    = "DBOwner"
      db_names     = alicloud_db_database.db_database.*.name
    }
  output.tf: |-
    # ------------------------------------------------------------------------------
    # Module Outputs
    #
    # This file defines the values returned to the caller after successful module execution.
    # These outputs can be referenced by other Terraform configurations or displayed to users after the apply command completes.
    # ------------------------------------------------------------------------------

    # Output public connection string of PostgreSQL instance
    output "pg_public_connection_string" {
      value       = alicloud_db_connection.public.connection_string
      description = "Public connection string of PostgreSQL"
    }

    # Outputs the database username.
    output "account_name" {
      value       = var.rds_db_user
      description = "Database Username"
    }

    # Outputs the database password.
    output "account_password" {
      value       = var.db_password
      sensitive   = true
      description = "Database Password"
    }

    # Outputs the database name.
    output "dbname" {
      value       = var.db_name
      description = "Database Name"
    }
