ROSTemplateFormatVersion: '2015-09-01'
Transform: Aliyun::Terraform-v1.5
Metadata:
  ALIYUN::ROS::Interface:
    TemplateTags:
      - acs:technical-solution:database:RDS+ClickHouse for Building One-Stop HTAP-tech_solu_19
Workspace:
  variable.tf: |-
    # ------------------------------------------------------------------------------
    # Module Input Variables
    #
    # This file defines all configurable input variables for this Terraform module.
    # Each variable contains detailed descriptions to help users configure the module correctly.
    # ------------------------------------------------------------------------------
    
    # Database instance type
    variable "db_instance_class" {
      type        = string
      default     = "8C32G"
      validation {
        condition     = contains(["8C32G", "16C64G", "32C128G","64C256G"], var.db_instance_class)
        error_message = "Invalid configuration information, please check and re-enter"
      }
      description = "Database instance specifications, please choose from the following specifications [8C32G, 16C64G, 32C128G, 64C256G]"
    }
    
    # RDS database username
    variable "rds_db_user" {
      type        = string
      default     = "rds_user"
      validation {
        condition     = can(regex("^[a-z][a-z0-9_]{0,30}[a-z0-9]$", var.rds_db_user))
        error_message = "The database username must start with a letter, end with a letter or number, and can only contain letters, numbers, and underscores up to 32 characters"
      }
      description = "RDS database account"
    }
    
    # Database password
    variable "db_password" {
      type        = string
      description = "Database password"
      #default    = ""
      validation {
        condition     = can(regex("^[0-9A-Za-z_!@#$%^&*()_+\\-=\\+]+$", var.db_password)) && length(var.db_password) >= 8 && length(var.db_password) <= 30
        error_message = "Length 8-30, must include three items (uppercase letters, lowercase letters, numbers, special symbols from !@#$%^&*()_+-=)"
      }
    }
    
    # ClickHouse database username
    variable "click_house_user" {
      type        = string
      default     = "ck_user"
      validation {
        condition     = can(regex("^[a-z][a-z0-9_]{0,30}[a-z0-9]$", var.click_house_user))
        error_message = "The database username must start with a letter, end with a letter or number, and can only contain letters, numbers, and underscores up to 32 characters"
      }
      description = "ClickHouse database account"
    }
    
    # ECS instance type
    variable "ecs_instance_type" {
      type        = string
      default     = "ecs.e-c1m2.large"
      description = "Instance type"
    }
    
    # ECS instance password
    variable "ecs_instance_password" {
      type        = string
      description = "ECS instance password"
      #default    = ""
      validation {
        condition     = can(regex("^[0-9A-Za-z_!@#$%^&*()_+\\-=\\+]+$", var.ecs_instance_password)) && length(var.ecs_instance_password) >= 8 && length(var.ecs_instance_password) <= 30
        error_message = "Length 8-30, must include three items (uppercase letters, lowercase letters, numbers, special symbols from !@#$%^&*()_+-=)"
      }
    }
  main.tf: |-
    # ------------------------------------------------------------------------------
    # Main Resource Definitions
    #
    # This file contains the core infrastructure resources of the module.
    # The code here is responsible for creating and configuring all cloud resources based on input variables.
    # ------------------------------------------------------------------------------
    
    # Configures Alibaba Cloud provider
    provider "alicloud" {
      region = "cn-hangzhou"
    }
    
    # Create a random ID
    resource "random_id" "suffix" {
      byte_length = 8
    }
    
    # Defines local variables
    locals {
      common_name = "rdsmysql2ck-${random_id.suffix.id}"
      # Select the first common availability zone (or choose based on logic)
      database_class = {
        RDS={
            "8C32G"= "mysql.x4.xlarge.2c"
            "16C64G"= "mysql.x4.2xlarge.2c"
            "32C128G"= "mysql.x4.4xlarge.2c"
            "64C256G"= "mysql.x4.8xlarge.2c"
        }
        ClickHouse={
            "8C32G"="S8"
            "16C64G"= "S16"
            "32C128G"= "S32"
            "64C256G"= "S64"
        }
      }
      ecs_command = <<SHELL
    #!/bin/bash
    echo "export RDS_URL=${alicloud_db_instance.rds_instance.connection_string}" >> ~/.bash_profile
    echo "export RDS_USERNAME=${var.rds_db_user}" >> ~/.bash_profile
    echo "export RDS_PASSWORD=${var.db_password}" >> ~/.bash_profile
    echo "export ROS_DEPLOY=true" >> ~/.bash_profile
    source ~/.bash_profile
    curl -fsSL https://help-static-aliyun-doc.aliyuncs.com/install-script/rdsclickhouse-htap/install_htap.sh|sh
    SHELL
    }
    
    # Query availability zones
    data "alicloud_db_zones" "zones_ids" {
      engine                   = "MySQL"
      engine_version           = "8.0"
      instance_charge_type     = "PostPaid"
      category                 = "Basic"
      db_instance_storage_type = "cloud_essd"
    }
    
    # Create a VPC
    resource "alicloud_vpc" "vpc" {
      vpc_name = "vpc_${local.common_name}"
      cidr_block = "192.168.0.0/16"
    }
    
    # Create a VSwitch
    resource "alicloud_vswitch" "vswitch" {     
      vpc_id = alicloud_vpc.vpc.id
      zone_id = data.alicloud_db_zones.zones_ids.ids.1
      cidr_block = "192.168.0.0/24"
      vswitch_name = "vswitch_${local.common_name}"
    }
    
    # Create a Security Group
    resource "alicloud_security_group" "security_group" {
      vpc_id = alicloud_vpc.vpc.id
    }
    
    # Create an RDS database instance
    resource "alicloud_db_instance" "rds_instance" {
      instance_name = "rdsmysql_${local.common_name}"
      engine = "MySQL"
      instance_storage = 100
      engine_version = "8.0"
      category = "HighAvailability"
      db_instance_storage_type = "cloud_essd"
      security_ips = ["0.0.0.0/0"]
      vpc_id = alicloud_vpc.vpc.id
      zone_id = data.alicloud_db_zones.zones_ids.ids.1
      vswitch_id = alicloud_vswitch.vswitch.id
      instance_charge_type = "Postpaid"
      instance_type = lookup(local.database_class["RDS"], var.db_instance_class, "rds.mysql.s1.small")
    }
    
    # Create a database
    resource "alicloud_db_database" "database" {
      character_set   = "utf8mb4"
      instance_id     = alicloud_db_instance.rds_instance.id
      name            = "tpcc"
      description     = "tpcc_${local.common_name}"
    }
    
    # Create a database account
    resource "alicloud_db_account" "default" {
      db_instance_id      = alicloud_db_instance.rds_instance.id
      account_name        = var.rds_db_user
      account_password    = var.db_password
      account_type        = "Super"
    }
    
    # Create a ClickHouse database cluster
    resource "alicloud_click_house_db_cluster" "click_house" {
      db_cluster_version      = "23.8"
      category                = "Basic"
      db_cluster_class        = lookup(local.database_class["ClickHouse"], var.db_instance_class, "rds.mysql.s1.small")
      db_cluster_network_type = "vpc"
      db_node_group_count     = "1"
      payment_type            = "PayAsYouGo"
      db_node_storage         = "100"
      storage_type            = "cloud_essd"
      vswitch_id              = alicloud_vswitch.vswitch.id
      vpc_id                  = alicloud_vpc.vpc.id
      zone_id                 = data.alicloud_db_zones.zones_ids.ids.1
      db_cluster_description  = "ck_${local.common_name}"
    }
    
    # Create a ClickHouse database account
    resource "alicloud_click_house_account" "default" {
      db_cluster_id       = alicloud_click_house_db_cluster.click_house.id
      account_name        = var.click_house_user
      account_password    = var.db_password
      type                = "Super"
    }
    
    # Retrieve the latest CentOS 7.9 system image
    data "alicloud_images" "centos_7_9" {
      name_regex  = "^centos_7_9_x64_*"
      owners      = "system"  # Official image
      most_recent = true      # Get the latest version
    }
    
    # Create an ECS instance
    resource "alicloud_instance" "ecs_instance" {
      availability_zone    = data.alicloud_db_zones.zones_ids.ids.1
      vpc_id               = alicloud_vpc.vpc.id
      vswitch_id           = alicloud_vswitch.vswitch.id
      internet_max_bandwidth_out = 5
      security_groups      = [alicloud_security_group.security_group.id]
      password             = var.ecs_instance_password
      instance_type        = var.ecs_instance_type
      system_disk_category = "cloud_essd"
      image_id             = data.alicloud_images.centos_7_9.images[0].id
    }
    
    # Create ECS command resource
    resource "alicloud_ecs_command" "run_tpcc_alicloud_ecs_command" {
      name             = "commond_install"
      description      = "commond_install_description"
      type             = "RunShellScript"
      command_content  = base64encode(local.ecs_command)
      timeout          = 3600
      working_dir      = "/root"
    }
    
    # Create ECS invocation resource
    resource "alicloud_ecs_invocation" "run_tpcc_alicloud_ecs_invocation" {
      instance_id = [alicloud_instance.ecs_instance.id]
      command_id = alicloud_ecs_command.run_tpcc_alicloud_ecs_command.id
      depends_on = [alicloud_db_instance.rds_instance]
      timeouts {
        create = "60m"
      }
    }
  output.tf: |-
    # ------------------------------------------------------------------------------
    # Module Outputs
    #
    # This file defines the values returned to the caller after successful module execution.
    # These outputs can be referenced by other Terraform configurations or displayed to users after the apply command completes.
    # ------------------------------------------------------------------------------
    
    # Outputs the ID of the created VPC resource
    output "vpc_id" {
      value = alicloud_vpc.vpc.id
    }
    
    # Outputs the ID of the created RDS instance
    output "rds_instance_id" {
      value = alicloud_db_instance.rds_instance.id
    }
    
    # Outputs the ID of the created ClickHouse cluster
    output "click_house_cluster_id" {
      value = alicloud_click_house_db_cluster.click_house.id
    }
    
    # Outputs the ID of the created ECS instance
    output "ecs_instance_id" {
      value = alicloud_instance.ecs_instance.id
    }

    # Outputs the database username
    output "db_username" {
      value       = var.rds_db_user
      description = "RDS database username"
    }

    # Outputs the database password
    output "db_password" {
      value       = var.db_password
      description = "RDS database password"
    }

    # Outputs the ClickHouse database username
    output "ck_db_name" {
      value       = var.click_house_user
      description = "ClickHouse database username"
    }    

    # Outputs the ClickHouse database password
    output "ck_db_password" {
      value       = var.db_password
      description = "ClickHouse database password"
    }    

    # Outputs the Ecs instance password
    output "ecs_instance_password" {
      value       = var.ecs_instance_password
      description = "ECS instance password"
    } 