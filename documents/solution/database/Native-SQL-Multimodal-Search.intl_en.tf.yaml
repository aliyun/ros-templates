ROSTemplateFormatVersion: '2015-09-01'
Transform: Aliyun::Terraform-v1.5
Metadata:
  ALIYUN::ROS::Interface:
    TemplateTags:
      - acs:technical-solution:internet-application-development:Native SQL for Easy Multimodal Intelligent Search-tech_solu_262
Workspace:
  variable.tf: |-
    # ------------------------------------------------------------------------------
    # Module Input Variables
    #
    # This file defines all configurable input variables for this Terraform module.
    # Each variable contains detailed descriptions to help users configure the module correctly.
    # ------------------------------------------------------------------------------

    # Name of the OSS bucket
    variable "bucket_name" {
      type        = string
      description = "bucket_name, please keep unique within the same availability zone"
      default     = "test-bucket-polar"
    }

    # Username for PolarDB
    variable "account_name" {
      type        = string
      default     = "polar_ai"
      description = "account_name"
      validation {
        condition     = can(regex("^[a-z][a-z0-9_]{0,30}[a-z0-9]$", var.account_name))
        error_message = "The database username must start with a letter, end with a letter or number, and can only contain letters, numbers, and underscores with a maximum of 32 characters"
      }
    }

    # Password for PolarDB
    variable "db_password" {
      type        = string
      sensitive   = true
      description = "account_password"
      #default    = ""
      validation {
        condition     = can(regex("^[0-9A-Za-z_!@#$%^&*()_+\\-=\\+]+$", var.db_password)) && length(var.db_password) >= 8 && length(var.db_password) <= 30
        error_message = "Length 8-30, must include three items (uppercase letters, lowercase letters, numbers, special symbols from !@#$%^&*()_+-=)"
      }
    }

    # Name of the PolarDB database
    variable "dbname" {
      type        = string
      default     = "db-test"
      description = "dbname"
      validation {
        condition     = can(regex("^[a-z][a-z0-9_-]{0,62}[a-z0-9]$", var.dbname))
        error_message = "Composed of lowercase letters, numbers, hyphens (-), and underscores (_), ending with a lowercase letter or number, starting with a letter, ending with a letter or number, up to 64 characters"
      }
    }

  main.tf: |-
    # ------------------------------------------------------------------------------
    # Main Resource Definitions
    #
    # This file contains the core infrastructure resources of the module.
    # The code here is responsible for creating and configuring all cloud resources based on input variables.
    # ------------------------------------------------------------------------------

    # Configure Alibaba Cloud provider
    provider "alicloud" {
      region = "cn-beijing"
    }

    # Generate random string for naming
    resource "random_string" "lowercase" {
      length  = 8
      special = false
      upper   = false
      numeric = false
    }

    # Generate random ID for naming
    resource "random_id" "suffix" {
      byte_length = 8
    }

    # Define local variable for naming convention
    locals {
      common_name = "SmartSearch-${random_id.suffix.id}"
    }

    # Query availability zones
    data "alicloud_polardb_node_classes" "default" {
      db_type    = "PostgreSQL"
      pay_type   = "PostPaid"
      category   = "SENormal"
    }

    # Create VPC
    resource "alicloud_vpc" "vpc" {
      cidr_block = "192.168.0.0/16"
      vpc_name   = "VPC_${local.common_name}"
    }

    # Create VSwitch
    resource "alicloud_vswitch" "vswitch" {
      vpc_id       = alicloud_vpc.vpc.id
      cidr_block   = "192.168.1.0/24"
      zone_id      = data.alicloud_polardb_node_classes.default.classes[0].zone_id
      vswitch_name = "vsw_001_${local.common_name}"
    }

    # Create PolarDB cluster
    resource "alicloud_polardb_cluster" "polardb_cluster" {
      vpc_id              = alicloud_vpc.vpc.id
      db_type             = "PostgreSQL"
      vswitch_id          = alicloud_vswitch.vswitch.id
      db_version          = "14"
      creation_category   = "SENormal"
      storage_space       = 20
      hot_standby_cluster = "OFF"
      db_node_class       = "polar.pg.g2.2xlarge.c"
      pay_type            = "PostPaid"
      storage_type        = "ESSDPL1"
      security_ips        = ["0.0.0.0/0"]
      db_node_num         = 2
    }

    # Create PolarDB account
    resource "alicloud_polardb_account" "account" {
      db_cluster_id    = alicloud_polardb_cluster.polardb_cluster.id
      account_name     = var.account_name
      account_password = var.db_password
      account_type     = "Super"
    }

    # Query PolarDB endpoints
    data "alicloud_polardb_endpoints" "polardb_endpoints" {
      db_cluster_id = alicloud_polardb_cluster.polardb_cluster.id
    }

    # Create PolarDB endpoint address
    resource "alicloud_polardb_endpoint_address" "dbcluster_endpoint_address" {
      db_endpoint_id = data.alicloud_polardb_endpoints.polardb_endpoints.endpoints[0].db_endpoint_id
      db_cluster_id  = alicloud_polardb_cluster.polardb_cluster.id
      net_type       = "Public"
      depends_on = [
        alicloud_polardb_account.account
      ]
    }

    # Create PolarDB database
    resource "alicloud_polardb_database" "polardb_database" {
      db_cluster_id      = alicloud_polardb_cluster.polardb_cluster.id
      db_name            = var.dbname
      character_set_name = "utf8"
      account_name       = var.account_name
    }

    # Enable OSS service
    data "alicloud_oss_service" "open_oss" {
      enable = "On"
    }

    # Create OSS bucket
    resource "alicloud_oss_bucket" "ossbucket" {
      bucket          = "${var.bucket_name}-${random_string.lowercase.result}"
      force_destroy   = true
      redundancy_type = "ZRS"
    }

  output.tf: |-
    # ------------------------------------------------------------------------------
    # Module Outputs
    #
    # This file defines the values returned to the caller after successful module execution.
    # These outputs can be referenced by other Terraform configurations or displayed to users after the apply command completes.
    # ------------------------------------------------------------------------------

    # Output OSS bucket name
    output "bucket_name" {
      value       = alicloud_oss_bucket.ossbucket.bucket
      description = "OSS bucket name"
    }

    # Output PolarDB database username
    output "account_name" {
      value       = var.account_name
      description = "PolarDB database username"
    }

    # Output PolarDB database password
    output "account_password" {
      value       = var.db_password
      sensitive   = true
      description = "PolarDB database password"
    }

    # Output PolarDB database name
    output "dbname" {
      value       = var.dbname
      description = "PolarDB database name"
    }

    # Output PolarDB public address
    output "connection_string" {
      value       = format("%s:5432", alicloud_polardb_endpoint_address.dbcluster_endpoint_address.connection_string)
      description = "Public address of the PolarDB database"
    }
