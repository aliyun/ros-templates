ROSTemplateFormatVersion: '2015-09-01'
Transform: Aliyun::Terraform-v1.5
Metadata:
  ALIYUN::ROS::Interface:
    TemplateTags:
      - acs:technical-solution:ecs:Cost-Effective Real-Time Log Storage and Retrieval Analysis with SelectDB-tech_solu_225
Workspace:
  variables.tf: |-
    # ------------------------------------------------------------------------------
    # Module Input Variables
    #
    # This file defines all configurable input variables for this Terraform module.
    # Each variable contains detailed descriptions to help users configure the module correctly.
    # ------------------------------------------------------------------------------

    # Specifies the ECS instance type
    variable "instance_type" {
      type        = string
      default     = "ecs.g8i.4xlarge"
      description = "ECS instance type, recommended to choose an instance with 16 vCPU and 64 GiB configuration"
    }

    # ECS server root account password
    variable "ecs_instance_password" {
      type        = string
      description = "ECS server root account password"
      #default    = ""
      validation {
        condition     = can(regex("^[0-9A-Za-z_!@#$%^&*()_+\\-=\\+]+$", var.ecs_instance_password)) && length(var.ecs_instance_password) >= 8 && length(var.ecs_instance_password) <= 30
        error_message = "Length 8-30, must include three items (uppercase letters, lowercase letters, numbers, special symbols from !@#$%^&*()_+-=)"
      }
    }

    # SelectDB instance specification
    variable "instance_class" {
      type        = string
      default     = "selectdb.4xlarge"
      description = "SelectDB instance specification"
    }

    # SelectDB engine version
    variable "selectdb_engine_version" {
      type        = string
      default     = "4.0.4"
      description = "SelectDB engine version"
      validation {
        condition = contains(["3.0.12", "4.0.4"], var.selectdb_engine_version)
        error_message = "Invalid configuration information, please check and re-enter"
      }
    }

    # SelectDB admin account password
    variable "db_password" {
      type        = string
      description = "SelectDB admin account password"
      #default    = ""
      validation {
        condition     = can(regex("^[0-9A-Za-z_!@#$%^&*()_+\\-=\\+]+$", var.db_password)) && length(var.db_password) >= 8 && length(var.db_password) <= 30
        error_message = "Length 8-30, must include three items (uppercase letters, lowercase letters, numbers, special symbols from !@#$%^&*()_+-=)"
      }
    }

  main.tf: |-
    # ------------------------------------------------------------------------------
    # Main Resource Definitions
    # 
    # This file contains the core infrastructure resources of the module.
    # The code here is responsible for creating and configuring all cloud resources based on input variables.
    # ------------------------------------------------------------------------------

    # Configure the Alibaba Cloud provider
    provider "alicloud" {
      region = "cn-hangzhou"
    }

    # Create a random ID for resource naming
    resource "random_id" "suffix" {
      byte_length = 8
    }

    # Define local variable common_name
    locals {
      common_name = "SelectDB-${random_id.suffix.id}"
      ecs_command = <<SHELL
    #!/bin/bash
    cd /root
    export ROS_DEPLOY=true
    wget https://help-static-aliyun-doc.aliyuncs.com/install-script/selectdb-observability/yc_log_demo_2.0.1.tar.gz
    tar -zxvf yc_log_demo_2.0.1.tar.gz
    cd /root/log_demo
    bash install.sh
    sudo chown -R root:root /root/log_demo
    SHELL
    }

    # Query availability zones
    data "alicloud_zones" "default" {
      available_disk_category     = "cloud_essd"
      available_instance_type     = var.instance_type
    }

    # Create a VPC
    resource "alicloud_vpc" "vpc_select" {
      cidr_block = "192.168.0.0/16"
      vpc_name       = "${local.common_name}-vpc"
    }

    # Create a VSwitch
    resource "alicloud_vswitch" "vsw_select" {
      vpc_id     = alicloud_vpc.vpc_select.id
      zone_id    = data.alicloud_zones.default.ids[0]
      cidr_block = "192.168.0.0/24"
      vswitch_name       = "${local.common_name}-vsw"
    }

    # Create a Security Group
    resource "alicloud_security_group" "sg_select" {
      security_group_name   = "${local.common_name}-sg"
      vpc_id = alicloud_vpc.vpc_select.id
    }

    # Retrieve Alibaba Cloud official image
    data "alicloud_images" "image_id" {
      name_regex  = "^aliyun_3_9_x64_20G*"
      owners      = "system"  # Official image
      most_recent = true      # Get the latest version
    }

    # Create an ECS instance
    resource "alicloud_instance" "ecs_instance" {
      vpc_id                     = alicloud_vpc.vpc_select.id
      vswitch_id                 = alicloud_vswitch.vsw_select.id
      security_groups = [alicloud_security_group.sg_select.id]
      image_id                   = data.alicloud_images.image_id.images[0].id
      instance_name              = "${local.common_name}-ecs"
      instance_type              = var.instance_type
      system_disk_category       = "cloud_essd"
      system_disk_size           = 100
      internet_max_bandwidth_out = 10
      password                   = var.ecs_instance_password
    }

    # Create a SelectDB instance
    resource "alicloud_selectdb_db_instance" "selectdb_instance" {
      vpc_id                  = alicloud_vpc.vpc_select.id
      zone_id                 = data.alicloud_zones.default.ids[0]
      vswitch_id              = alicloud_vswitch.vsw_select.id
      db_instance_description = "${local.common_name}-selectdb"
      db_instance_class       = var.instance_class
      cache_size              = 100
      payment_type            = "PayAsYouGo"
      engine_minor_version    = var.selectdb_engine_version
      admin_pass              = var.db_password
      desired_security_ip_lists {
        group_name            = "default"
        security_ip_list      = "192.168.0.0/16"
      }
    }

    # Create an ECS command resource
    resource "alicloud_ecs_command" "run_tpcc_alicloud_ecs_command" {
      name             = "commond_install"
      description      = "commond_install_description"
      type             = "RunShellScript"
      command_content  = base64encode(local.ecs_command)
      timeout          = 3600
      working_dir      = "/root"
    }

    # Create an ECS command invocation resource
    resource "alicloud_ecs_invocation" "run_tpcc_alicloud_ecs_invocation" {
      instance_id = [alicloud_instance.ecs_instance.id]
      command_id = alicloud_ecs_command.run_tpcc_alicloud_ecs_command.id
      depends_on = [alicloud_selectdb_db_instance.selectdb_instance]
      timeouts {
        create = "60m"
      }
    }
  outputs.tf: |-
    # ------------------------------------------------------------------------------
    # Module Outputs
    #
    # This file defines the values returned to the caller after successful module execution.
    # These outputs can be referenced by other Terraform configurations or displayed to users after the apply command completes.
    # ------------------------------------------------------------------------------

    # Outputs the ECS login user
    output "ecs_account" {
      description = "ECS login user"
      value       = "root"
    }

    # Outputs the ECS user root password
    output "ecs_login_password" {
      description = "ECS user root password"
      value       = var.ecs_instance_password
      sensitive   = true
    }

    # Outputs the SelectDB login user
    output "selectdb_account" {
      description = "SelectDB login user"
      value       = "admin"
    }

    # Outputs the SelectDB user admin password
    output "selectdb_login_password" {
      description = "SelectDB user admin password"
      value       = var.db_password
      sensitive   = true
    }

    # Outputs the SelectDB VPC connection string
    output "selectdb_vpc_connection_string" {
      description = "SelectDB VPC connection string"
      value       = alicloud_selectdb_db_instance.selectdb_instance.instance_net_infos[1].connection_string
    }
