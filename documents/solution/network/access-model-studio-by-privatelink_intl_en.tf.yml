ROSTemplateFormatVersion: '2015-09-01'
Transform: Aliyun::Terraform-v1.5
Metadata:
  ALIYUN::ROS::Interface:
    TemplateTags:
      - acs:technical-solution:access-model-studio-by-privatelink:通过私有网络安全高效访问百炼服务-tech_solu_240
Workspace:
  main.tf: |
    # ------------------------------------------------------------------------------
    # Core Resource Definitions (Main Resource Definitions)
    #
    # This file contains the core infrastructure resources of the module.
    # The code here is responsible for creating and configuring all cloud resources 
    # based on input variables.
    # ------------------------------------------------------------------------------

    data "alicloud_cen_transit_router_service" "open" {
      enable = "On"
    }

    data "alicloud_privatelink_service" "open" {
      enable = "On"
    }

    data "alicloud_pvtz_service" "open" {
      enable = "On"
    }

    # Malaysia Region
    provider "alicloud" {
      alias  = "region_malaysia"
      region = "ap-southeast-3"
    }

    # Singapore Region
    provider "alicloud" {
      alias  = "region_singapore"
      region = "ap-southeast-1"
    }

    # Generate random suffix to ensure unique resource names
    resource "random_id" "suffix" {
      byte_length = 8
    }

    # Local variable definitions
    locals {
      common_name = random_id.suffix.id  # Common name prefix for all resources
    }

    # Malaysia VPC
    resource "alicloud_vpc" "vpc_my" {
      provider   = alicloud.region_malaysia
      cidr_block = "192.168.0.0/16"
      vpc_name   = "my-vpc-${local.common_name}"
    }

    # Malaysia Availability Zone 1 VSwitch
    resource "alicloud_vswitch" "vsw1_my" {
      provider     = alicloud.region_malaysia
      vpc_id       = alicloud_vpc.vpc_my.id
      cidr_block   = "192.168.1.0/24"
      zone_id      = var.my_zone_id1
      vswitch_name = "my-vsw1-${local.common_name}"
    }

    # Security Group
    resource "alicloud_security_group" "sg_my" {
      provider            = alicloud.region_malaysia
      security_group_name = "my-sg-${local.common_name}"
      vpc_id              = alicloud_vpc.vpc_my.id
    }

    # Allow Cloud Assistant connection (SSH)
    resource "alicloud_security_group_rule" "allow_workbench" {
      provider          = alicloud.region_malaysia
      type              = "ingress"
      ip_protocol       = "tcp"
      port_range        = "22/22"
      cidr_ip           = "100.104.0.0/16"
      security_group_id = alicloud_security_group.sg_my.id
    }

    # Malaysia ECS Instance
    resource "alicloud_instance" "ecs_my" {
      provider             = alicloud.region_malaysia
      instance_name        = "ecs-1-${local.common_name}"
      image_id             = "aliyun_3_9_x64_20G_alibase_20231219.vhd"
      instance_type        = var.instance_type
      system_disk_category = "cloud_essd"
      system_disk_size     = 40
      vswitch_id           = alicloud_vswitch.vsw1_my.id
      security_groups      = [alicloud_security_group.sg_my.id]
      password             = var.ecs_instance_password
    }

    # Singapore VPC
    resource "alicloud_vpc" "vpc_sg" {
      provider   = alicloud.region_singapore
      cidr_block = "172.16.0.0/16"
      vpc_name   = "sg-vpc-${local.common_name}"
    }

    # Singapore Availability Zone 1 VSwitch
    resource "alicloud_vswitch" "vsw1_sg" {
      provider     = alicloud.region_singapore
      vpc_id       = alicloud_vpc.vpc_sg.id
      cidr_block   = "172.16.1.0/24"
      zone_id      = var.sg_zone_id1
      vswitch_name = "sg-vsw1-${local.common_name}"
    }

    # Singapore Availability Zone 2 VSwitch
    resource "alicloud_vswitch" "vsw2_sg" {
      provider     = alicloud.region_singapore
      vpc_id       = alicloud_vpc.vpc_sg.id
      cidr_block   = "172.16.2.0/24"
      zone_id      = var.sg_zone_id2
      vswitch_name = "sg-vsw2-${local.common_name}"
    }

    # Security Group
    resource "alicloud_security_group" "sg_sg" {
      provider            = alicloud.region_singapore
      security_group_name = "sg-sg-${local.common_name}"
      vpc_id              = alicloud_vpc.vpc_sg.id
    }

    # Allow HTTP access from Malaysia VPC
    resource "alicloud_security_group_rule" "http_ingress_rule" {
      provider          = alicloud.region_singapore
      type              = "ingress"
      ip_protocol       = "tcp"
      nic_type          = "intranet"
      policy            = "accept"
      port_range        = "80/80"
      priority          = 1
      security_group_id = alicloud_security_group.sg_sg.id
      cidr_ip           = alicloud_vpc.vpc_my.cidr_block
    }

    # Allow HTTPS access from Malaysia VPC
    resource "alicloud_security_group_rule" "https_ingress_rule" {
      provider          = alicloud.region_singapore
      type              = "ingress"
      ip_protocol       = "tcp"
      nic_type          = "intranet"
      policy            = "accept"
      port_range        = "443/443"
      priority          = 1
      security_group_id = alicloud_security_group.sg_sg.id
      cidr_ip           = alicloud_vpc.vpc_my.cidr_block
    }

    # CEN Instance - Cross-region network connection
    resource "alicloud_cen_instance" "cen" {
      cen_instance_name = "cen-${local.common_name}"
      description       = "CEN instance for cross-region connectivity with route synchronization"
    }

    # Malaysia Transit Router
    resource "alicloud_cen_transit_router" "my-tr" {
      provider            = alicloud.region_malaysia
      cen_id              = alicloud_cen_instance.cen.id
      transit_router_name = "my-tr-${local.common_name}"
      depends_on          = [data.alicloud_cen_transit_router_service.open]
    }

    # Singapore Transit Router
    resource "alicloud_cen_transit_router" "sg-tr" {
      provider            = alicloud.region_singapore
      cen_id              = alicloud_cen_instance.cen.id
      transit_router_name = "sg-tr-${local.common_name}"
      depends_on          = [data.alicloud_cen_transit_router_service.open]
    }

    # Malaysia VPC connection to Malaysia Transit Router
    resource "alicloud_cen_transit_router_vpc_attachment" "my_vpc_attachment" {
      provider          = alicloud.region_malaysia
      cen_id            = alicloud_cen_instance.cen.id
      transit_router_id = alicloud_cen_transit_router.my-tr.transit_router_id
      vpc_id            = alicloud_vpc.vpc_my.id
      
      # Multi-availability zone mapping
      zone_mappings {
        zone_id    = var.my_zone_id1
        vswitch_id = alicloud_vswitch.vsw1_my.id
      }
      transit_router_vpc_attachment_name = "my-vpc-attachment-${local.common_name}"
      auto_publish_route_enabled         = true
    }

    # Singapore VPC connection to Singapore Transit Router
    resource "alicloud_cen_transit_router_vpc_attachment" "sg_vpc_attachment" {
      provider          = alicloud.region_singapore
      cen_id            = alicloud_cen_instance.cen.id
      transit_router_id = alicloud_cen_transit_router.sg-tr.transit_router_id
      vpc_id            = alicloud_vpc.vpc_sg.id
      
      # Multi-availability zone mapping
      zone_mappings {
        zone_id    = var.sg_zone_id1
        vswitch_id = alicloud_vswitch.vsw1_sg.id
      }
      zone_mappings {
        zone_id    = var.sg_zone_id2
        vswitch_id = alicloud_vswitch.vsw2_sg.id
      }
      transit_router_vpc_attachment_name = "sg-vpc-attachment-${local.common_name}"
      auto_publish_route_enabled         = true
    }

    # Malaysia Transit Router system route table query
    data "alicloud_cen_transit_router_route_tables" "my-tr-rt" {
      provider                        = alicloud.region_malaysia
      transit_router_id               = alicloud_cen_transit_router.my-tr.transit_router_id
      transit_router_route_table_type = "System"
    }

    # Singapore Transit Router system route table query
    data "alicloud_cen_transit_router_route_tables" "sg-tr-rt" {
      provider                        = alicloud.region_singapore
      transit_router_id               = alicloud_cen_transit_router.sg-tr.transit_router_id
      transit_router_route_table_type = "System"
    }

    # Extract system route table IDs
    locals {
      my_system_route_table_id = data.alicloud_cen_transit_router_route_tables.my-tr-rt.tables[0].transit_router_route_table_id
      sg_system_route_table_id = data.alicloud_cen_transit_router_route_tables.sg-tr-rt.tables[0].transit_router_route_table_id
    }

    # Malaysia to Singapore cross-region connection
    resource "alicloud_cen_transit_router_peer_attachment" "cen-tr-peer-attachment" {
      provider                            = alicloud.region_malaysia
      cen_id                              = alicloud_cen_instance.cen.id
      transit_router_id                   = alicloud_cen_transit_router.my-tr.transit_router_id
      peer_transit_router_region_id       = "ap-southeast-1"
      peer_transit_router_id              = alicloud_cen_transit_router.sg-tr.transit_router_id
      bandwidth_type                      = "DataTransfer"
      bandwidth                           = 5
      transit_router_peer_attachment_name = "peer-attachment-${local.common_name}"
      auto_publish_route_enabled          = true
    }

    # Malaysia VPC connection association to Malaysia system route table
    resource "alicloud_cen_transit_router_route_table_association" "my_vpc_attachment" {
      provider                      = alicloud.region_malaysia
      transit_router_route_table_id = local.my_system_route_table_id
      transit_router_attachment_id  = alicloud_cen_transit_router_vpc_attachment.my_vpc_attachment.transit_router_attachment_id
    }

    # Singapore VPC connection association to Singapore system route table
    resource "alicloud_cen_transit_router_route_table_association" "sg_vpc_attachment" {
      provider                      = alicloud.region_singapore
      transit_router_route_table_id = local.sg_system_route_table_id
      transit_router_attachment_id  = alicloud_cen_transit_router_vpc_attachment.sg_vpc_attachment.transit_router_attachment_id
    }

    # Cross-region connection association to Singapore system route table
    resource "alicloud_cen_transit_router_route_table_association" "sg_peer_attachment" {
      provider                      = alicloud.region_singapore
      transit_router_route_table_id = local.sg_system_route_table_id
      transit_router_attachment_id  = alicloud_cen_transit_router_peer_attachment.cen-tr-peer-attachment.transit_router_attachment_id
    }

    # Cross-region connection association to Malaysia system route table
    resource "alicloud_cen_transit_router_route_table_association" "my_peer_attachment" {
      provider                      = alicloud.region_malaysia
      transit_router_route_table_id = local.my_system_route_table_id
      transit_router_attachment_id  = alicloud_cen_transit_router_peer_attachment.cen-tr-peer-attachment.transit_router_attachment_id
    }

    # Malaysia VPC route propagation to Malaysia system route table
    resource "alicloud_cen_transit_router_route_table_propagation" "my_vpc_propagation" {
      provider                      = alicloud.region_malaysia
      transit_router_route_table_id = local.my_system_route_table_id
      transit_router_attachment_id  = alicloud_cen_transit_router_vpc_attachment.my_vpc_attachment.transit_router_attachment_id
    }

    # Singapore VPC route propagation to Singapore system route table
    resource "alicloud_cen_transit_router_route_table_propagation" "sg_vpc_propagation" {
      provider                      = alicloud.region_singapore
      transit_router_route_table_id = local.sg_system_route_table_id
      transit_router_attachment_id  = alicloud_cen_transit_router_vpc_attachment.sg_vpc_attachment.transit_router_attachment_id
    }

    # Cross-region connection route propagation to Singapore system route table
    resource "alicloud_cen_transit_router_route_table_propagation" "sg_peer_propagation" {
      provider                      = alicloud.region_singapore
      transit_router_route_table_id = local.sg_system_route_table_id
      transit_router_attachment_id  = alicloud_cen_transit_router_peer_attachment.cen-tr-peer-attachment.transit_router_attachment_id
    }

    # Cross-region connection route propagation to Malaysia system route table
    resource "alicloud_cen_transit_router_route_table_propagation" "my_peer_propagation" {
      provider                      = alicloud.region_malaysia
      transit_router_route_table_id = local.my_system_route_table_id
      transit_router_attachment_id  = alicloud_cen_transit_router_peer_attachment.cen-tr-peer-attachment.transit_router_attachment_id
    }

    # PrivateLink
    resource "alicloud_privatelink_vpc_endpoint" "dashscope_endpoint" {
      provider           = alicloud.region_singapore
      service_name       = "com.aliyuncs.dashscope"
      vpc_endpoint_name  = "dashscope-endpoint-${local.common_name}"
      security_group_ids = [alicloud_security_group.sg_sg.id]
      vpc_id             = alicloud_vpc.vpc_sg.id
      depends_on         = [data.alicloud_privatelink_service.open,alicloud_security_group.sg_sg]
    }

    # Endpoint availability zone 1 configuration
    resource "alicloud_privatelink_vpc_endpoint_zone" "zone1" {
      provider    = alicloud.region_singapore
      endpoint_id = alicloud_privatelink_vpc_endpoint.dashscope_endpoint.id
      vswitch_id  = alicloud_vswitch.vsw1_sg.id
      zone_id     = var.sg_zone_id1
    }

    # Endpoint availability zone 2 configuration
    resource "alicloud_privatelink_vpc_endpoint_zone" "zone2" {
      provider    = alicloud.region_singapore
      endpoint_id = alicloud_privatelink_vpc_endpoint.dashscope_endpoint.id
      vswitch_id  = alicloud_vswitch.vsw2_sg.id
      zone_id     = var.sg_zone_id2
    }

    # Private DNS configuration
    resource "alicloud_pvtz_zone" "dashscope_pvtz_zone" {
      provider  = alicloud.region_malaysia
      zone_name = "vpc-ap-southeast-1.dashscope.aliyuncs.com"
      depends_on = [data.alicloud_pvtz_service.open]
    }

    # DNS CNAME record pointing to Singapore PrivateLink endpoint
    resource "alicloud_pvtz_zone_record" "dashscope_cname_record" {
      provider = alicloud.region_malaysia
      zone_id  = alicloud_pvtz_zone.dashscope_pvtz_zone.id
      value    = alicloud_privatelink_vpc_endpoint.dashscope_endpoint.endpoint_domain
      rr       = "@"
      type     = "CNAME"
      ttl      = 60
      status   = "ENABLE"
    }

    # Bind Malaysia VPC to private DNS zone
    resource "alicloud_pvtz_zone_attachment" "my_vpc_attachment" {
      provider = alicloud.region_malaysia
      zone_id  = alicloud_pvtz_zone.dashscope_pvtz_zone.id
      vpcs {
        vpc_id    = alicloud_vpc.vpc_my.id
        region_id = "ap-southeast-3"
      }
    }
  outputs.tf: |
    # ------------------------------------------------------------------------------
    # Module Outputs (Module Outputs)
    #
    # This file defines the values returned to the caller after successful 
    # module execution. These outputs can be referenced by other Terraform 
    # configurations or displayed to users after the apply command completes.
    # ------------------------------------------------------------------------------

    # ECS login address
    output "ecs_login_address" {
      description = "ECS login address"
      value       = format("https://ecs-workbench.aliyun.com/?from=EcsConsole&instanceType=ecs&regionId=%s&instanceId=%s", "ap-southeast-3", alicloud_instance.ecs_my.id)
    }
  variables.tf: |
    # ------------------------------------------------------------------------------
    # Module Input Variables (Module Input Variables)
    #
    # This file defines all configurable input variables for this Terraform module.
    # Each variable includes detailed 'description' to explain its purpose, format 
    # and default value logic. Please refer to these descriptions to configure 
    # the module correctly.
    # ------------------------------------------------------------------------------

    # Singapore region availability zone 1
    variable "sg_zone_id1" {
      type        = string
      description = "Singapore availability zone 1"
      default     = "ap-southeast-1a"
    }

    # Singapore region availability zone 2
    variable "sg_zone_id2" {
      type        = string
      description = "Singapore availability zone 2"
      default     = "ap-southeast-1b"
    }

    # ECS instance type
    variable "instance_type" {
      type        = string
      description = "ECS instance type"
      default     = "ecs.e-c1m2.large"
    }

    # ECS instance login password
    variable "ecs_instance_password" {
      type        = string
      sensitive   = true
      description = "Server login password, length 8-30, must contain three types (uppercase letters, lowercase letters, numbers, special symbols)"
    }

    # Malaysia region availability zone 1
    variable "my_zone_id1" {
      type        = string
      description = "Malaysia availability zone 1"
      default     = "ap-southeast-3a"
    }
