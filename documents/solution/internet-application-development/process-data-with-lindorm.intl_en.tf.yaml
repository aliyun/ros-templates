ROSTemplateFormatVersion: '2015-09-01'
Transform: Aliyun::Terraform-v1.5
Metadata:
  ALIYUN::ROS::Interface:
    TemplateTags:
      - acs:technical-solution:internet-application-development:Lindorm General Time-Series Data One-Stop Processing-tech_solu_141
Workspace:
  variable.tf: |-
    # ------------------------------------------------------------------------------
    # Module Input Variables
    #
    # This file defines all configurable input variables for this Terraform module.
    # Each variable contains detailed descriptions to help users configure the module correctly.
    # ------------------------------------------------------------------------------

    # Specifies the ECS instance type.
    variable "instance_type" {
      type = string
      description = "ECS instance type"
      default = "ecs.e-c1m4.2xlarge"
    }

    # ECS instance password.
    variable "instance_password" {
      type = string
      sensitive = true
      validation {
        condition     = can(regex("^[0-9A-Za-z_!@#$%^&*()_+\\-=\\+]+$", var.instance_password)) && length(var.instance_password) >= 8 && length(var.instance_password) <= 30
        error_message = "Length 8-30, must include three items (uppercase letters, lowercase letters, numbers, special symbols from !@#$%^&*()_+-=)"
      }
      description = "ECS instance password"
      #default = ""
    }

  main.tf: |-
    # ------------------------------------------------------------------------------
    # Main Resource Definitions
    # 
    # This file contains the core infrastructure resources of the module.
    # The code here is responsible for creating and configuring all cloud resources based on input variables.
    # ------------------------------------------------------------------------------

    # Configure Alibaba Cloud provider.
    provider "alicloud" {
      region = "cn-hangzhou"
    }

    # Generate a random string.
    resource "random_string" "lowercase" {
      length  = 8
      special = false
      upper   = false
      numeric = false
    }

    # Define local variables.
    locals {
      common_name = "lindorm-demo-${random_string.lowercase.result}"
      sorted_zone_ids = sort(data.alicloud_zones.default.ids)
      max_zone_id     = local.sorted_zone_ids[length(local.sorted_zone_ids) - 1]
      
      # Define ECS startup command.
      ecs_command = <<-SHELL
              #!/bin/bash
              function log_info() {
                  printf "%s [INFO] %s\n" "$(date '+%Y-%m-%d %H:%M:%S')" "$1"
              }
              function log_error() {
                  printf "%s [ERROR] %s\n" "$(date '+%Y-%m-%d %H:%M:%S')" "$1"
              }
              function debug_exec(){
                  local cmd="$@"
                  log_info "$cmd"
                  eval "$cmd"
                  ret=$?
                  echo ""
                  log_info "$cmd, exit code: $ret"
                  return $ret
              }
              function init_work(){
                yum upgrade & yum install -y python3 cryptography==3.4.8
                wget -O lindorm-cli-linux-latest.tar.gz https://tsdbtools.oss-cn-hangzhou.aliyuncs.com/lindorm-cli-linux-latest.tar.gz
                tar zxvf lindorm-cli-linux-latest.tar.gz
              }
              debug_exec init_work
                SHELL
    }

    # Query availability zones.
    data "alicloud_zones" "default" {
      available_disk_category     = "cloud_essd"
      available_instance_type     = var.instance_type
    }

    # Create a VPC.
    resource "alicloud_vpc" "vpc" {
      cidr_block = "192.168.0.0/16"
      vpc_name = "vpc-${local.common_name}"
    }

    # Create a VSwitch.
    resource "alicloud_vswitch" "vswitch" {
      vpc_id = alicloud_vpc.vpc.id
      cidr_block = "192.168.0.0/24"
      zone_id = "${local.max_zone_id}"
      vswitch_name = "vsw-${local.common_name}"
    }

    # Create a security group.
    resource "alicloud_security_group" "security_group" {
      vpc_id = alicloud_vpc.vpc.id
      security_group_name = "sg-${local.common_name}"
    }

    # Query the latest image.
    data "alicloud_images" "alinux3" {
      name_regex  = "^aliyun_3_x64_*"
      owners      = "system"
      most_recent = true
      status = "Available"
    }

    # Create an ECS instance.
    resource "alicloud_instance" "ecs_instance" {
      availability_zone    = "${local.max_zone_id}"
      vpc_id               = alicloud_vpc.vpc.id
      vswitch_id           = alicloud_vswitch.vswitch.id
      security_groups      = [alicloud_security_group.security_group.id]
      password             = var.instance_password
      instance_type        = var.instance_type
      instance_name        = "ecs-${local.common_name}"
      system_disk_category = "cloud_essd"
      image_id             = data.alicloud_images.alinux3.images[0].id
      internet_max_bandwidth_out = 5
    }

    # Create a Lindorm instance.
    resource "alicloud_lindorm_instance" "lindorm_instance" {
      instance_storage = 160
      zone_id = "${local.max_zone_id}"
      payment_type = "PayAsYouGo"
      vswitch_id = alicloud_vswitch.vswitch.id
      vpc_id = alicloud_vpc.vpc.id
      search_engine_specification = "lindorm.g.xlarge"
      search_engine_node_count = 2
      table_engine_specification = "lindorm.g.xlarge"
      table_engine_node_count = 2
      disk_category = "cloud_efficiency"
      instance_name = "lindorm-${local.common_name}"
    }

    # Define ECS command resource.
    resource "alicloud_ecs_command" "deploy_application_on_ecs_alicloud_ecs_command" {
      type = "RunShellScript"
      timeout = 300
      command_content = base64encode(local.ecs_command)
      name = "auto-75ca2a13"
      working_dir      = "/root"
    }

    # Create ECS invocation.
    resource "alicloud_ecs_invocation" "deploy_application_on_ecs_alicloud_ecs_invocation" {
      instance_id = [alicloud_instance.ecs_instance.id]
      command_id = alicloud_ecs_command.deploy_application_on_ecs_alicloud_ecs_command.id
      timeouts {
        create = "60m"
      }
    }

  output.tf: |-
    # ------------------------------------------------------------------------------
    # Module Outputs
    #
    # This file defines the values returned to the caller after successful module execution.
    # These outputs can be referenced by other Terraform configurations or displayed to users after the apply command completes.
    # ------------------------------------------------------------------------------
    
    # Outputs the Lindorm instance id.
    output "lindorm_instance_id" {
      value       = alicloud_lindorm_instance.lindorm_instance.id
      description = "Lindorm instance id"
    }

    # Outputs the ECS instance id.
    output "ecs_instance_id" {
      value       = alicloud_instance.ecs_instance.id
      description = "ECS instance id"
    }


    # Outputs the ECS login address.
    output "ecs_login_address" {
      description = "ECS login address"
      value       = format("https://ecs-workbench.aliyun.com/?from=EcsConsole&instanceType=ecs&regionId=%s&instanceId=%s","cn-hangzhou",alicloud_instance.ecs_instance.id)
    }

    # Outputs the ECS instance username.
    output "ecs_instance_username" {
      value       = "root"
      description = "ECS instance username"
    }

    # Outputs the ECS instance password.
    output "ecs_instance_password" {
      value       = var.instance_password
      sensitive   = true
      description = "ECS instance password"
    }

