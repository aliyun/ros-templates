ROSTemplateFormatVersion: '2015-09-01'
Transform: Aliyun::Terraform-v1.5
Metadata:
  ALIYUN::ROS::Interface:
    TemplateTags:
      - acs:technical-solution:internet-application-development:应用日志数据归档-tech_solu_08
Workspace:
  variable.tf: |-
    # ------------------------------------------------------------------------------
    # Module Input Variables
    # ------------------------------------------------------------------------------

    variable "ecs_instance_type" {
      type        = string
      default     = "ecs.t6-c1m2.large"
      description = "ECS Instance Type"
    }

    # 用于登录ECS实例的密码。
    variable "ecs_instance_password" {
      type        = string
      sensitive   = true
      description = "ECS login password, 8-30 character, must include three of the followings(uppercase letters, lowercase letters, numbers, special characters including ()`~!@#$%^&*_-+=|{}[]:;'<>,.?/)"
      # default   = ""
    }

  main.tf: |-
    # ------------------------------------------------------------------------------
    # Main Resource Definitions
    # ------------------------------------------------------------------------------
    
    provider "alicloud" {
      region = "cn-hangzhou"
    }
    
    data "alicloud_regions" "current_region_ds" {
      current = true
    }

    data "alicloud_zones" "default" {
      available_disk_category     = "cloud_essd"
      available_resource_creation = "VSwitch"
      available_instance_type     = var.ecs_instance_type
    }

    resource "random_string" "suffix" {
      length  = 8
      lower   = true
      upper   = false
      numeric = false
      special = false
    }

    locals {
      common_name = random_string.suffix.id
      region = data.alicloud_regions.current_region_ds.regions.0.id
    }

    resource "alicloud_vpc" "vpc" {
      cidr_block = "192.168.0.0/16"
      vpc_name   = "vpc-${local.common_name}"
    }

    resource "alicloud_vswitch" "vswitch" {
      vpc_id       = alicloud_vpc.vpc.id
      cidr_block   = "192.168.0.0/24"
      zone_id      = data.alicloud_zones.default.zones.0.id
      vswitch_name = "vswitch-${local.common_name}"
    }

    resource "alicloud_security_group" "security_group" {
      vpc_id              = alicloud_vpc.vpc.id
      security_group_name = "sg-${local.common_name}"
    }

    resource "alicloud_security_group_rule" "allow_http" {
      type              = "ingress"
      ip_protocol       = "tcp"
      nic_type          = "intranet"
      policy            = "accept"
      port_range        = "80/80"
      priority          = 1
      security_group_id = alicloud_security_group.security_group.id
      cidr_ip           = "0.0.0.0/0"
    }

    resource "alicloud_security_group_rule" "allow_db" {
      type              = "ingress"
      ip_protocol       = "tcp"
      nic_type          = "intranet"
      policy            = "accept"
      port_range        = "3306/3306"
      priority          = 1
      security_group_id = alicloud_security_group.security_group.id
      cidr_ip           = "0.0.0.0/0"
    }

    data "alicloud_images" "default" {
      name_regex  = "^aliyun_3_x64_20G_alibase_.*"
      most_recent = true
      owners      = "system"
    }

    resource "alicloud_ram_user" "ram_user" {
      name = "ram-user-${local.common_name}"
    }

    resource "alicloud_ram_access_key" "ramak" {
      user_name = alicloud_ram_user.ram_user.name
    }

    resource "alicloud_ram_user_policy_attachment" "attach_policy_to_user" {
      user_name   = alicloud_ram_user.ram_user.name
      policy_type = "System"
      policy_name = "AliyunLogFullAccess"
    }

    resource "alicloud_instance" "ecs_instance" {
      instance_name              = "ecs-${local.common_name}"
      image_id                   = data.alicloud_images.default.images[0].id
      instance_type              = var.ecs_instance_type
      system_disk_category       = "cloud_essd"
      security_groups            = [alicloud_security_group.security_group.id]
      vswitch_id                 = alicloud_vswitch.vswitch.id
      password                   = var.ecs_instance_password
      internet_max_bandwidth_out = 5
    }

    resource "alicloud_ecs_command" "run_command_nginx_loongcollector" {
      name = "command-run-nginx-loongcollector-${local.common_name}"
      command_content = base64encode(<<EOF
    #!/bin/bash
    yum update -y
    yum install -y nginx
    systemctl start nginx
    systemctl enable nginx

    wget http://aliyun-observability-release-${local.region}.oss-${local.region}.aliyuncs.com/loongcollector/linux64/latest/loongcollector.sh -O loongcollector.sh
    chmod +x loongcollector.sh
    ./loongcollector.sh install ${local.region}-internet

    cat << EOJ >> genlog.sh
    echo "127.0.0.1 - - [\$(date +'%d/%b/%Y:%H:%M:%S %z')] \"GET /HTTP/1.1\" 200 4897 \"-\" \"Mozilla/5.0 (Macintosh; Intel Mac OS X 10_15_7) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/115.0.0.0 Safari/537.36\"" >> /var/log/nginx/access.log
    EOJ
    chmod +x genlog.sh

    cat << EOT >> crontest.cron
    * * * * * ./genlog.sh
    EOT

    crontab crontest.cron
    EOF
      )
      working_dir = "/root"
      type        = "RunShellScript"
      timeout     = 3600
    }

    resource "alicloud_ecs_invocation" "invoke_script_nginx_loongcollector" {
      instance_id = [alicloud_instance.ecs_instance.id]
      command_id  = alicloud_ecs_command.run_command_nginx_loongcollector.id
      timeouts {
        create = "15m"
      }
    }

    data "alicloud_log_service" "open" {
      enable = "On"
    }

    resource "alicloud_log_project" "sls_project" {
      project_name = "sls-project-${local.common_name}"
    }

    resource "alicloud_log_store" "sls_log_store" {
      logstore_name = "sls-logstore-${local.common_name}"
      project_name  = alicloud_log_project.sls_project.project_name
    }

    resource "alicloud_log_machine_group" "this" {
      identify_list = alicloud_instance.ecs_instance[*].primary_ip_address
      name          = "lmg-${local.common_name}"
      project       = alicloud_log_project.sls_project.project_name
      identify_type = "ip"
    }

    resource "alicloud_logtail_config" "this" {
      project      = alicloud_log_project.sls_project.project_name
      input_detail = <<EOF
    {
      "discardUnmatch": false,
      "enableRawLog": true,
      "fileEncoding": "utf8",
      "filePattern": "access.log",
      "logPath": "/var/log/nginx/",
      "logType": "common_reg_log",
      "maxDepth": 10,
      "topicFormat": "none"
    }
    EOF
      input_type   = "file"
      logstore     = alicloud_log_store.sls_log_store.logstore_name
      name         = "lc-${local.common_name}"
      output_type  = "LogService"
    }

    resource "alicloud_logtail_attachment" "this" {
      project             = alicloud_log_project.sls_project.project_name
      logtail_config_name = alicloud_logtail_config.this.name
      machine_group_name  = alicloud_log_machine_group.this.name
    }

    resource "alicloud_log_store_index" "sls_index" {
      project  = alicloud_log_project.sls_project.project_name
      logstore = alicloud_log_store.sls_log_store.logstore_name
      full_text {
        token = " :#$^*\r\n\t"
      }
      field_search {
        name  = "content"
        type  = "text"
        token = " :#$^*\r\n\t"
      }
    }

    data "alicloud_oss_service" "open" {
      enable = "On"
    }

    resource "alicloud_ram_role" "log_default_role" {
      role_name                   = "log-default-role-${local.common_name}"
      assume_role_policy_document = <<EOF
      {
      "Statement": [
        {
          "Action": "sts:AssumeRole",
          "Effect": "Allow",
          "Principal": {
            "Service": [
              "log.aliyuncs.com"
            ]
          }
        }
      ],
      "Version": "1"
      }
      EOF
    }

    resource "alicloud_ram_role_policy_attachment" "attach_policy_to_role" {
      role_name   = alicloud_ram_role.log_default_role.role_name
      policy_type = "System"
      policy_name = "AliyunLogRolePolicy"
    }

    resource "alicloud_oss_bucket" "oss_bucket" {
      bucket        = "bucket-${local.common_name}"
      storage_class = "IA"
      force_destroy = true
    }

    resource "alicloud_sls_oss_export_sink" "default" {
      project      = alicloud_log_project.sls_project.project_name
      display_name = "display-${local.common_name}"
      job_name     = "export-${local.common_name}"
      configuration {
        logstore  = alicloud_log_store.sls_log_store.logstore_name
        role_arn  = alicloud_ram_role.log_default_role.arn
        from_time = 1
        to_time   = 0
        sink {
          bucket           = alicloud_oss_bucket.oss_bucket.bucket
          buffer_interval  = "300"
          buffer_size      = "250"
          compression_type = "gzip"
          content_type     = "json"
          content_detail   = jsonencode({ "enableTag" : true })
          endpoint         = "https://oss-${local.region}-internal.aliyuncs.com"
          time_zone        = "+0800"
          role_arn         = alicloud_ram_role.log_default_role.arn
          prefix           = "app01"
          suffix           = ""
          path_format      = "%Y/%m/%d/%H/%M"
        }
      }
    }



  output.tf: |-
    # ------------------------------------------------------------------------------
    # Module Outputs
    # ------------------------------------------------------------------------------

    output "ecs_login_address" {
      description = "The login address of ECS instance, where the log is generated。After logging in, view the log：tail -f /var/log/nginx/access.log"
      value       = format("https://ecs-workbench.aliyun.com/?from=ecs&instanceType=ecs&regionId=%s&instanceId=%s&resourceGroupId=", local.region, alicloud_instance.ecs_instance.id)
    }
